start: toplevel* 
?toplevel: datasource | memmap | annotate

datasource: "datasource" dsname? properties?
dsname: NAME

memmap: "memmap" mmname ("(" mmdatasource ")")? mmbody?
mmname: NAME
mmdatasource: NAME
mmbody: "{" mmentry+ "}"
mmentry: range mmdecoder ("<" mmdataaddr)? properties?
mmdataaddr: mmfromaddr | mmfromreset
mmfromaddr: number
mmfromreset: "*"
mmdecoder: NAME

annotate: "annotate" "{" atoplevel* "}"
?atoplevel: label | comment
label:  range ("(" lflags+ ")")? lname
lflags: "i"
lname: NAME
comment: aaddress cpos? ctext
aaddress: NUMBER
cpos: cpost
cpost: "^"	-> u
	 | "v"	-> b
	 | ">"  -> i
ctext: TQUOTED | QUOTED

properties: "{" propentry* "}"
propentry: propname "=" propval
propname: NAME
propval: variant | list
?variant : boolean | name | number | string
name: NAME
number: NUMBER

string: QUOTED
boolean: TRUE | FALSE
list: variant ("," variant)+

range: single | (first "-" last)
single: rnumber
first: rnumber
last: rnumber
?rnumber: NUMBER

LINECOMMENT: "//"/.*/
BLOCKCOMMENT: /\/\*.*?\/*/s

NAMEFIRST: /[a-zA-Z_]/
NAMEREST: /[0-9a-zA-Z\-_]/
NAME: NAMEFIRST NAMEREST*

TRUE: "True"
FALSE: "False"
DECIMAL: /[0-9]+/
HEXNUM: /\$[0-9a-fA-F]+/
NUMBER: DECIMAL | HEXNUM

TSQUOTED: /'''.*?'''/s
TDQUOTED: /""".*?"""/s
TQUOTED: TSQUOTED | TDQUOTED
SQUOTED: /'.*?'/
DQUOTED: /".*?"/
QUOTED: SQUOTED | DQUOTED

%import common.WS
%ignore WS
%ignore LINECOMMENT
%ignore BLOCKCOMMENT
