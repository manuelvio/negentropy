start: toplevel* 
toplevel: datasource | memmap | annotate

datasource: "datasource" dsname? properties?
dsname: NAME

memmap: "memmap" mmname ("(" mmdatasource ")")? mmbody?
mmname: NAME
mmdatasource: NAME
mmbody: "{" mmentry+ "}"
mmentry: range mmdecoder ("<" mmdataaddr)? properties?
mmdataaddr: mmfromaddr | mmfromreset
mmfromaddr: number
mmfromreset: "*"
mmdecoder: NAME
mmfirst: number
mmlast: number

annotate: "annotate" "{" atoplevel* "}"
atoplevel: label | comment
label:  range ("(" lflags+ ")")? lname
lflags: "i"
lname: NAME
comment: aaddress WS* cpos? ctext
aaddress: range
cpos: "^" | "v" | ">"
ctext: TQUOTED | QUOTED

properties: "{" propentry* "}"
propentry: propname "=" propval
propname: NAME
propval: variant | list

variant : name | number | string | boolean
name: NAME
number: DECIMAL | HEXNUM
string: QUOTED
boolean: BOOLEAN
list: variant ("," variant)+

range: range_single | (range_first "-" range_last)
range_single: number
range_first: number
range_last: number

LINECOMMENT: "//"/.*/
BLOCKCOMMENT: /\/\*.*?\/*/s

NAMEFIRST: /[a-zA-Z_]/
NAMEREST: /[0-9a-zA-Z\-_]/
NAME: NAMEFIRST NAMEREST*

BOOLEAN: "True" | "False"
DECIMAL: /[0-9]+/
HEXNUM: /\$[0-9a-fA-F]+/

TSQUOTED: /'''.*?'''/s
TDQUOTED: /""".*?"""/s
TQUOTED: TSQUOTED | TDQUOTED
SQUOTED: /'.*?'/
DQUOTED: /".*?"/
QUOTED: SQUOTED | DQUOTED

%import common.WS
%ignore WS
%ignore LINECOMMENT
%ignore BLOCKCOMMENT
