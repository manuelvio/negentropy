start: toplevel* 
?toplevel: datasource | memmap | annotate

datasource: "datasource" properties

memmap: _MEMMAP mmbody
mmbody: "{" mmentry+ "}"
mmentry: range NAME ["<" mmdataaddr] properties?
?mmdataaddr: mmfromaddr | mmfromreset
?mmfromaddr: number
?mmfromreset: "*"
?mmdecoder: NAME

annotate: "annotate" "{" atoplevel* "}"
?atoplevel: label | comment

label:  range ["(" lflags+ ")"] lname
!lflags: "i"
?lname: NAME

comment: number cpos? ctext
!cpos: "^" | "v" | ">"
?ctext: anystring
?anystring: quoted | tquoted

?string: quoted
quoted: QUOTED
tquoted: TQUOTED

properties: "{" propentry* "}"
propentry: NAME "=" propval
?propval: variant | list
?variant : boolean | number | string
boolean: TRUE | FALSE

?number: hexnum | decimal
hexnum: HEXNUM
decimal: DECIMAL

list: variant ("," variant)+

range: number ["-" number]

_MEMMAP: /memmap(?=[^0-9a-zA-Z\-_]|$)/s

LINECOMMENT: "//"/.*/
BLOCKCOMMENT: /\/\*.*?\/*/s

NAME: /[a-zA-Z_][0-9a-zA-Z\-_]*/

TRUE: "True"
FALSE: "False"

DECIMAL: /[0-9]+/
HEXNUM: /\$[0-9a-fA-F]+/

TSQUOTED: /'''.*?'''/s
TDQUOTED: /""".*?"""/s
TQUOTED: TSQUOTED | TDQUOTED
SQUOTED: /'.*?'/
DQUOTED: /".*?"/
QUOTED: SQUOTED | DQUOTED

%import common.WS
%ignore WS
%ignore LINECOMMENT
%ignore BLOCKCOMMENT
