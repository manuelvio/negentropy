start: toplevel* 
?toplevel: datasource | memmap | annotate

datasource: _DATASOURCE NAME? properties?

memmap: _MEMMAP mmname ["(" mmdatasource ")"] mmbody?
mmname: NAME
mmdatasource: NAME
mmbody: "{" mmentry+ "}"
mmentry: range mmdecoder ["<" mmdataaddr] properties?
mmdataaddr: mmfromaddr | mmfromreset
mmfromaddr: number
mmfromreset: "*"
mmdecoder: NAME

annotate: "annotate" "{" atoplevel* "}"
?atoplevel: label | comment
label:  range ["(" lflags+ ")"] lname
lflags: "i"
lname: NAME
comment: aaddress cpos? ctext
aaddress: NUMBER
cpos: cpost
cpost: "^"	-> a
	 | "v"	-> b
	 | ">"  -> i
ctext: TQUOTED | QUOTED

properties: "{" propentry* "}"
propentry: NAME "=" propval
?propval: variant | list
?variant : boolean | NAME | number | string
boolean: TRUE | FALSE

?number: hexnum | decimal
hexnum: HEXNUM
decimal: DECIMAL

string: QUOTED
list: variant ("," variant)+

range: single | (first "-" last)
single: rnumber
first: rnumber
last: rnumber
?rnumber: NUMBER

_DATASOURCE: /datasource(?=[^0-9a-zA-Z\-_]|$)/s
_MEMMAP: /memmap(?=[^0-9a-zA-Z\-_]|$)/s

LINECOMMENT: "//"/.*/
BLOCKCOMMENT: /\/\*.*?\/*/s

NAME: /[a-zA-Z_][0-9a-zA-Z\-_]*/

TRUE: "True"
FALSE: "False"
DECIMAL: /[0-9]+/
HEXNUM: /\$[0-9a-fA-F]+/
NUMBER: DECIMAL | HEXNUM

TSQUOTED: /'''.*?'''/s
TDQUOTED: /""".*?"""/s
TQUOTED: TSQUOTED | TDQUOTED
SQUOTED: /'.*?'/
DQUOTED: /".*?"/
QUOTED: SQUOTED | DQUOTED

%import common.WS
%ignore WS
%ignore LINECOMMENT
%ignore BLOCKCOMMENT
